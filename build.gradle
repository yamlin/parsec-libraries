/**
 * @author waynewu
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

allprojects {

    group = 'com.yahoo.parsec'

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
        jcenter()
    }

}

subprojects {
    apply plugin: 'com.jfrog.bintray'

    def versionPropsFile = file('version.properties')
    def versionPrefix = '0.0.0.'
    def versionBuild
  
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')

        publications = ['UnofficialPublication']
        
        publish = true
        override = true
        pkg {
            repo = 'gradle'
            name = project.name
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/yahoo/parsec-libraries'
            userOrg = 'yamlin'
            version {
                name = 'bintray'
                desc = 'The Parsec Libraries'
                released = new java.util.Date()
                vcsTag = '0.0.1'
            }
        }
    }

    publishing {
        publications {
            UnofficialPublication(MavenPublication) {
                from components.java
                groupId 'com.test.yamlin'
                artifactId project.name
    //            version versionPrefix + versionBuild
            }
        }
    }

    bintrayUpload.dependsOn check
}
